#!/bin/csh
# Copyright 2013 MBARI, all rights reserved. 
# For license and copyright details, see COPYRIGHT.TXT in the SIAM project
# home directory.

# Plot JVM memory as function of time

set terminal=
set output=
# Process leading options
while ($#argv > 0) 
  if ("$argv[1]" == "-terminal") then
    shift
    set terminal = `echo set terminal $argv[1]`
  else if ("$argv[1]" == "-output") then
    shift
    set output = `echo set output \"$argv[1]\"`
  else
    break    # end of options
  endif
  shift

end

if ($#argv < 2) then
  echo usage: `basename $0` '[-terminal type][-output file] nodeID portalLog'
  exit 1
endif


set nodeID=$argv[1]
set portalLog=$argv[2]

set awkfile=/tmp/plotMemory.awk

cat > $awkfile <<'EOF'

BEGIN {

  # Node's ISI ID
  target = ARGV[1];
  ARGV[1] = "";

  FS=",";
  nCycles = 0;
  # printf("packet#   total     free     used\n");
}

/devid=/ {
  if (index($0, target) == 0) {
    # Not our target
    inTarget = 0;
    next;
  }
  inTarget = 1;

  t = substr($2, 4)/1000;

  #  printf "t = %d\n", t;

  if (t0 == 0) {
    t0 = t;
  }

  elapsed = t - t0;
}

/devid=.*, t=.*/ {
  packetHeader = $0;
}

/jvmmem:/ {
  if (!inTarget) {
    next;
  }
  # Get timestamp from packet header
  ind = index(packetHeader, "t=") + 2;
  timeStampSec = substr(packetHeader, ind, 14)/1000.;

  i = index($0, ":");

  # Parse total and free memory, calculate used memory
  start = index($0, "total=") + 6;
  stop = index($0, ", ");
  total = substr($0, start, stop - start);
  start = index($0, "free=") + 5;
  stop = index($0, "(bytes)") - 1;
  free = substr($0, start, stop - start);
  printf "%d  %d  %d  %d\n", timeStampSec, total, free, total-free;

}
'EOF'

set datafile=/tmp/memory.dat
awk -f $awkfile $nodeID $portalLog > $datafile

set plotScript=/tmp/memory.gnuplot

cat > $plotScript << EOF2
$terminal
$output
set title "Total and used JVM memory"
set xdata time
set timefmt "%s"
set format x "%m/%d\n%H:%M"

set xlabel "Time (UTC)"
set ylabel "MBytes"
plot "$datafile" using 1:2 title "Total memory" with linespoints, "$datafile" using 1:4 title "Used memory" with linespoints

EOF2

gnuplot -persist $plotScript


